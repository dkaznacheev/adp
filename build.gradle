buildscript {
    ext.kotlin_version = "1.3.61"
    ext.coroutines_version = "1.3.3"
    ext.protobuf_version = "3.11.1"
    ext.grpc_version = "1.28.1" // CURRENT_GRPC_VERSION
    ext.grpc_kotlin_version = "0.1.1" // CURRENT_GRPC_KOTLIN_VERSION

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.11"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    google()
    jcenter()
    mavenCentral()
    mavenLocal()
}

apply plugin: "kotlin"
apply plugin: "com.google.protobuf"

apply plugin: "idea"
apply plugin: "application"

dependencies {
    // This repo
    implementation "io.grpc:grpc-kotlin-stub:$grpc_kotlin_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"

    // Grpc and Protobuf
    implementation "com.google.protobuf:protobuf-java:$protobuf_version"
    implementation "com.google.protobuf:protobuf-java-util:$protobuf_version"
    implementation "io.grpc:grpc-netty-shaded:$grpc_version"
    implementation "io.grpc:grpc-protobuf:$grpc_version"
    implementation "io.grpc:grpc-stub:$grpc_version"

    // Java
    compileOnly "javax.annotation:javax.annotation-api:1.2"

    // Google
    implementation "com.google.guava:guava:28.2-jre"

    implementation "org.jetbrains.kotlin:kotlin-compiler-embeddable:1.3.61"
    implementation "org.jetbrains.kotlin:kotlin-scripting-compiler-embeddable:1.3.61"
    implementation "org.jetbrains.kotlin:kotlin-scripting-jvm-host-embeddable:1.3.61"
    implementation "org.jetbrains.kotlin:kotlin-scripting-jvm:1.3.61"


    implementation "io.ktor:ktor-server-core:1.3.0"
    implementation "io.ktor:ktor-server-netty:1.3.0"

    compile "io.ktor:ktor-client-apache:1.3.0"

    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.10'

    compile group: 'com.esotericsoftware', name: 'kryo', version: '4.0.2'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:$protobuf_version" }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpc_version"
        }
        grpckt {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:$grpc_kotlin_version"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc { }
                grpckt { }
            }
        }
    }
}

startScripts.enabled = false

task adpClient(type: CreateStartScripts) {
    mainClassName = "io.grpc.examples.adp.ADPClientKt"
    applicationName = "adp-client"
    outputDir = startScripts.outputDir
    classpath = startScripts.classpath
}

task adpServer(type: CreateStartScripts) {
    mainClassName = "io.grpc.examples.adp.ADPServerKt"
    applicationName = "adp-server"
    outputDir = startScripts.outputDir
    classpath = startScripts.classpath
}

applicationDistribution.into("bin") {
    from(adpClient)
    from(adpServer)
    fileMode = 0755
}